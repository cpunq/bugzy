apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0


repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
    maven {
        url "https://jitpack.io"
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"


    defaultConfig {
        applicationId "com.bluestacks.bugzy"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                renameAPK(variant, defaultConfig, "release")
            }
        }
        debug {
            applicationVariants.all { variant ->
                renameAPK(variant, defaultConfig, "debug")
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def renameAPK(variant, defaultConfig, buildType) {
    variant.outputs.all { output ->
        def fileName = appName + "_v" + defaultConfig.versionName + "_" + buildType + ".apk"
        outputFileName = fileName
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    // Android support libs
    implementation 'com.android.support:appcompat-v7:26.1.0'

    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'

    // for view injection
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // dependency injection
    implementation 'com.google.dagger:dagger-android:2.11'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.11'
    implementation 'com.google.dagger:dagger-android-support:2.11' // if you use the support libraries
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    // Android arch components
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:1.0.0"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0"

    // Room
    implementation 'android.arch.persistence.room:runtime:1.1.0-beta1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.0-beta1'

    // UI
    implementation 'com.xiaofeng.android:flowlayoutmanager:1.2.3.2'
    implementation 'com.gauravbhola.ripplepulsebackground:library:1.0.0'

    implementation 'com.flipboard:bottomsheet-core:1.5.3'
    implementation 'com.flipboard:bottomsheet-commons:1.5.3'

    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.pkmmte.view:circularimageview:1.1'
    implementation 'com.guardanis:imageloader:1.3.5'
    implementation 'com.commonsware.cwac:richedit:0.5.+'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jsibbold:zoomage:1.1.0'

    implementation ('com.github.bumptech.glide:glide:4.6.1') {
        exclude group: "com.android.support"
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
}

