buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 1
ext.versionClassifier = ""

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
    maven {
        url "https://jitpack.io"
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    signingConfigs {
        release {
            storeFile file("./bugzy_upload_key")
            storePassword "bugbug"
            keyAlias "bugzy"
            keyPassword "bugbug"
            v2SigningEnabled false
        }
    }
    defaultConfig {
        applicationId "in.bugzy"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode project.ext.versionMajor * 10000 + project.ext.versionMinor * 100 + project.ext.versionPatch
        versionName generateVersionName()
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        setProperty("archivesBaseName", appName + "_v" + versionName )
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && ext.versionClassifier != "") {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:2.+'
    // Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:2.0.0-alpha1"


    implementation('com.mikepenz:aboutlibraries:6.0.8@aar') {
        transitive = true
    }

    // Android support libs
    implementation 'androidx.appcompat:appcompat:1.0.0-alpha3'
    implementation 'androidx.cardview:cardview:1.0.0-alpha3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-alpha3'
    implementation 'com.google.android.material:material:1.0.0-alpha3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-alpha3'

    // for view injection
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    // dependency injection
    implementation 'com.google.dagger:dagger-android:2.14'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.14'
    implementation 'com.google.dagger:dagger-android-support:2.14'
    // if you use the support libraries
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14'

    // Android arch components
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0-alpha1"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.0.0-alpha1"

    // Room
    implementation 'androidx.room:room-runtime:2.0.0-alpha1'
    annotationProcessor 'androidx.room:room-compiler:2.0.0-alpha1'

    // UI
    implementation 'com.xiaofeng.android:flowlayoutmanager:1.2.3.2'
    implementation 'com.gauravbhola.ripplepulsebackground:library:1.0.0'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation "com.hootsuite.android:nachos:1.1.1"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.1'


    implementation 'com.flipboard:bottomsheet-core:1.5.3'
    implementation 'com.flipboard:bottomsheet-commons:1.5.3'

    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.pkmmte.view:circularimageview:1.1'

    implementation('com.github.bumptech.glide:glide:4.6.1') {
        exclude group: "com.android.support"
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'

    // Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
}

